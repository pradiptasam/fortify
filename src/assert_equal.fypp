#:def assert_equal(type, suffix)
  subroutine assert_equal_${suffix}$(expected, actual, test_name, num_failures)
    ${type}$, intent(in) :: expected, actual
    character(len=*), intent(in) :: test_name
    integer, intent(inout) :: num_failures

    #:if suffix == "logical"
    if (expected .neqv. actual) then
    #:else
    if (expected /= actual) then
    #:endif
        print *, "FAIL: ", test_name, " - Expected: ", expected, " but got: ", actual
        num_failures = num_failures + 1
    else
        print *, "PASS: ", test_name
    end if
  end subroutine assert_equal_${suffix}$

#:enddef
#:def assert_not_equal(type, suffix)
  subroutine assert_not_equal_${suffix}$(expected, actual, test_name, num_failures)
    ${type}$, intent(in) :: expected, actual
    character(len=*), intent(in) :: test_name
    integer, intent(inout) :: num_failures

    #:if suffix == "logical"
    if (expected .neqv. actual) then
    #:else
    if (expected /= actual) then
    #:endif
        print *, "PASS: ", test_name
    else
        print *, "FAIL: ", test_name, " - Expected: ", expected, " but got: ", actual
        num_failures = num_failures + 1
    end if
  end subroutine assert_not_equal_${suffix}$

#:enddef
#:def assert_less_than(type, suffix)
  subroutine assert_less_than_${suffix}$(val1, val2, test_name, num_failures)
    ${type}$, intent(in) :: val1, val2
    character(len=*), intent(in) :: test_name
    integer, intent(inout) :: num_failures

    if (val1 < val2) then
        print *, "PASS: ", test_name
    else
        print *, "FAIL: ", test_name, " - Expected less than ", val2, " but got: ", val1
        num_failures = num_failures + 1
    end if
  end subroutine assert_less_than_${suffix}$

#:enddef
#:def assert_greater_than(type, suffix)
  subroutine assert_greater_than_${suffix}$(val1, val2, test_name, num_failures)
    ${type}$, intent(in) :: val1, val2
    character(len=*), intent(in) :: test_name
    integer, intent(inout) :: num_failures

    if (val1 > val2) then
        print *, "PASS: ", test_name
    else
        print *, "FAIL: ", test_name, " - Expected greater than ", val2, " but got: ", val1
        num_failures = num_failures + 1
    end if
  end subroutine assert_greater_than_${suffix}$

#:enddef
#:def assert_less_than_equal(type, suffix)
  subroutine assert_less_than_equal_${suffix}$(val1, val2, test_name, num_failures)
    ${type}$, intent(in) :: val1, val2
    character(len=*), intent(in) :: test_name
    integer, intent(inout) :: num_failures

    if (val1 <= val2) then
        print *, "PASS: ", test_name
    else
        print *, "FAIL: ", test_name, " - Expected less than equal", val2, " but got: ", val1
        num_failures = num_failures + 1
    end if
  end subroutine assert_less_than_equal_${suffix}$

#:enddef
#:def assert_greater_than_equal(type, suffix)
  subroutine assert_greater_than_equal_${suffix}$(val1, val2, test_name, num_failures)
    ${type}$, intent(in) :: val1, val2
    character(len=*), intent(in) :: test_name
    integer, intent(inout) :: num_failures

    if (val1 >= val2) then
        print *, "PASS: ", test_name
    else
        print *, "FAIL: ", test_name, " - Expected greater than equal", val2, " but got: ", val1
        num_failures = num_failures + 1
    end if
  end subroutine assert_greater_than_equal_${suffix}$

#:enddef
#:def assert_array_near(type, suffix)
    ! 1D array comparison
    subroutine assert_array_near_${suffix}$_1d(array1, array2, tolerance, test_name, num_failures)
        ${type}$, dimension(:), intent(in) :: array1, array2
        ${type}$, intent(in) :: tolerance
        character(len=*), intent(in) :: test_name
        integer, intent(inout) :: num_failures
        integer :: i

        if (size(array1) /= size(array2)) then
            print *, "FAIL:", test_name, "- Arrays have different sizes."
            num_failures = num_failures + 1
            return
        end if

        do i = 1, size(array1)
            if (abs(array1(i) - array2(i)) > tolerance) then
                print *, "FAIL:", test_name, "- Arrays differ at index", i, ": expected", array1(i), "but got", array2(i)
                num_failures = num_failures + 1
                return
            end if
        end do
        print *, "PASS:", test_name
    end subroutine assert_array_near_${suffix}$_1d

    ! 2D array comparison
    subroutine assert_array_near_${suffix}$_2d(array1, array2, tolerance, test_name, num_failures)
        ${type}$, dimension(:,:), intent(in) :: array1, array2
        ${type}$, intent(in) :: tolerance
        character(len=*), intent(in) :: test_name
        integer, intent(inout) :: num_failures
        integer :: i, j

        if (size(array1, 1) /= size(array2, 1) .or. size(array1, 2) /= size(array2, 2)) then
            print *, "FAIL:", test_name, "- Arrays have different dimensions."
            num_failures = num_failures + 1
            return
        end if

        do i = 1, size(array1, 1)
            do j = 1, size(array1, 2)
                if (abs(array1(i, j) - array2(i, j)) > tolerance) then
                    print *, "FAIL:", test_name, "- Arrays differ at index (", i, ",", j, "): expected", array1(i, j), "but got", array2(i, j)
                    num_failures = num_failures + 1
                    return
                end if
            end do
        end do
        print *, "PASS:", test_name
    end subroutine assert_array_near_${suffix}$_2d

#:enddef
module check_assertions

  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: sp => real32, &
    &                                      dp => real64

  implicit none

  ! Define the generic interface for assert_equal
  interface assert_equal
    #:for suffix in ["integer", "real_sp", "real_dp", "logical", "character_len_star"]
    module procedure assert_equal_${suffix}$
    #:endfor
  end interface

  interface assert_not_equal
    #:for suffix in ["integer", "real_sp", "real_dp", "logical", "character_len_star"]
    module procedure assert_not_equal_${suffix}$
    #:endfor
  end interface

  interface assert_less_than
    #:for suffix in ["integer", "real_sp", "real_dp"]
    module procedure assert_less_than_${suffix}$
    #:endfor
  end interface

  interface assert_greater_than
    #:for suffix in ["integer", "real_sp", "real_dp"]
    module procedure assert_greater_than_${suffix}$
    #:endfor
  end interface

  interface assert_less_than_equal
    #:for suffix in ["integer", "real_sp", "real_dp"]
    module procedure assert_less_than_equal_${suffix}$
    #:endfor
  end interface

  interface assert_greater_than_equal
    #:for suffix in ["integer", "real_sp", "real_dp"]
    module procedure assert_greater_than_equal_${suffix}$
    #:endfor
  end interface

  interface assert_array_near
    #:for suffix in ["real_sp", "real_dp"]
    module procedure assert_array_near_${suffix}$_1d
    module procedure assert_array_near_${suffix}$_2d
    #:endfor
  end interface

contains

    ! Generate assert_equal subroutines for different types
    #:for t, suffix in [["integer", "integer"], ["real(dp)", "real_dp"], ["real(sp)", "real_sp"], ["logical", "logical"], ["character(len=*)", "character_len_star"]]
    #:call assert_equal(t, suffix)
    #:endcall
    #:endfor

    #:for t, suffix in [["integer", "integer"], ["real(dp)", "real_dp"], ["real(sp)", "real_sp"], ["logical", "logical"], ["character(len=*)", "character_len_star"]]
    #:call assert_not_equal(t, suffix)
    #:endcall
    #:endfor

    #:for t, suffix in [["integer", "integer"], ["real(dp)", "real_dp"], ["real(sp)", "real_sp"]]
    #:call assert_less_than(t, suffix)
    #:endcall
    #:endfor

    #:for t, suffix in [["integer", "integer"], ["real(dp)", "real_dp"], ["real(sp)", "real_sp"]]
    #:call assert_greater_than(t, suffix)
    #:endcall
    #:endfor

    #:for t, suffix in [["integer", "integer"], ["real(dp)", "real_dp"], ["real(sp)", "real_sp"]]
    #:call assert_less_than_equal(t, suffix)
    #:endcall
    #:endfor

    #:for t, suffix in [["integer", "integer"], ["real(dp)", "real_dp"], ["real(sp)", "real_sp"]]
    #:call assert_greater_than_equal(t, suffix)
    #:endcall
    #:endfor

    subroutine assert_true(condition, test_name, num_failures)
        logical, intent(in) :: condition
        character(len=*), intent(in) :: test_name
        integer, intent(inout) :: num_failures

        if (.not. condition) then
            print *, "FAIL:", test_name, "- Expected true but got false"
            num_failures = num_failures + 1
        else
            print *, "PASS:", test_name
        end if
    end subroutine assert_true

    subroutine assert_false(condition, test_name, num_failures)
        logical, intent(in) :: condition
        character(len=*), intent(in) :: test_name
        integer, intent(inout) :: num_failures

        if (condition) then
            print *, "FAIL:", test_name, "- Expected false but got true"
            num_failures = num_failures + 1
        else
            print *, "PASS:", test_name
        end if
    end subroutine assert_false

    #:for t, suffix in [["real(dp)", "real_dp"], ["real(sp)", "real_sp"]]
    #:call assert_array_near(t, suffix)
    #:endcall
    #:endfor

end module check_assertions
